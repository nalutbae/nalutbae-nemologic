plugins {
    id 'java'
}

repositories {
    mavenCentral()
}

group 'com.story4g.nalutbae'
version '0.0.1-SNAPSHOT'

dependencies {
    // https://mvnrepository.com/artifact/org.jsoup/jsoup
    implementation 'org.jsoup:jsoup:1.9.2'
    // https://mvnrepository.com/artifact/com.googlecode.json-simple/json-simple
    implementation 'com.googlecode.json-simple:json-simple:1.1.1'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

// Create jar with dependencies
jar {
    duplicatesStrategy = 'exclude'
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest {
        attributes(
                'Main-Class': 'com.story4g.nalutbae.nemologic.NemonemologicDotComQuizParser'
        )
    }
    archiveFileName.set("${project.name}-${project.version}-dep.jar")
    archiveClassifier.set('')
}

// Create jar with no dependencies
task createJarWithoutDependencies(type: Jar) {
    from sourceSets.main.output
    archiveClassifier.set('nodeps')
    archiveFileName.set("${project.name}-${project.version}.jar")
}

build {
    dependsOn createJarWithoutDependencies
}